apply from: rootProject.file("gradle/component.gradle")

apply plugin: 'kotlin-android'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
        buildConfigField "int", "SDK_TARGET_VERSION", "29"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    //单元测试用
    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }

    defaultPublishConfig "pickerDebug"

    flavorDimensions "function", "region"
    productFlavors {
        lite {
            dimension "function"
            buildConfigField "boolean", "IS_TOB", "true"
        }
        picker {
            dimension "function"
            buildConfigField "boolean", "IS_TOB", "true"
        }
        china {
            dimension "region"
            buildConfigField "boolean", "IS_I18N", "false"
        }
        global {
            dimension "region"
            buildConfigField "boolean", "IS_I18N", "true"
        }
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java', '../device_register/src/main/java']
            aidl.srcDirs = ['src/main/aidl', '../device_register/src/main/aidl']
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation 'com.bytedance.applog:plugin-aggregation:0.0.1'
    compileOnly 'com.google.android.gms:play-services:6.5.87'
    compileOnly fileTree(include: ['*.jar'], dir: 'libs')
    compileOnly 'androidx.recyclerview:recyclerview:1.0.0'
    compileOnly 'androidx.fragment:fragment:1.0.0'
    compileOnly 'androidx.appcompat:appcompat:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.1'
    androidTestImplementation "androidx.test.ext:junit:1.1.4"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.61'
    implementation "com.bytedance.applog:RangersAppLog-Log:1.0.18"

}
