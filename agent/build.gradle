apply from: rootProject.file("gradle/component.gradle")

apply plugin: 'kotlin-android'

def getGitHash = {->
  def stdout = new ByteArrayOutputStream()
  exec {
    commandLine 'git', 'rev-parse', '--short', 'HEAD'
    standardOutput = stdout
  }
  return stdout.toString().trim()
}

android {
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  defaultConfig {
    buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
    buildConfigField "int", "SDK_TARGET_VERSION", "29"
  }

  buildTypes {
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }

  //单元测试用
  testOptions {
    unitTests {
      returnDefaultValues = true
      includeAndroidResources = true
    }
  }

  defaultPublishConfig "pickerDebug"

  flavorDimensions "function", "region"
  productFlavors {
    lite {
      dimension "function"
    }
    picker {
      dimension "function"
    }
    china {
      dimension "region"
      buildConfigField "boolean", "IS_I18N", "false"
    }
    global {
      dimension "region"
      buildConfigField "boolean", "IS_I18N", "true"
    }
  }

  lintOptions {
    abortOnError false
  }
}

dependencies {
  compileOnly 'com.google.android.gms:play-services:6.5.87'
  compileOnly fileTree(include: ['*.jar'], dir: 'libs')
  compileOnly 'com.android.support:appcompat-v7:28.0.0'
  compileOnly 'com.android.support:support-annotations:28.0.0'
  compileOnly 'com.android.support:recyclerview-v7:28.0.0'
  compileOnly 'androidx.recyclerview:recyclerview:1.0.0'
  compileOnly 'androidx.fragment:fragment:1.0.0'
  compileOnly 'androidx.appcompat:appcompat:1.0.0'
  pickerImplementation 'com.android.support:appcompat-v7:28.0.0'
  liteImplementation 'com.android.support:appcompat-v7:28.0.0'
  testImplementation 'junit:junit:4.12'
  androidTestImplementation 'com.android.support.test:rules:1.0.2'
  androidTestImplementation 'com.android.support.test:runner:1.0.2'
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

  implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.61'
}
